
locals {
  http_port    = 80
  any_port     = 0
  any_protocol = "-1"
  tcp_protocol = "tcp"
  all_ips      = ["0.0.0.0/0"]
}

resource "aws_launch_configuration" "example" {
  
  image_id = "ami-08c40ec9ead489470"
  instance_type = var.instance_type
  security_groups = [aws_security_group.instance.id,aws_security_group.terrassh.id]
  key_name = "linux"

  user_data = templatefile("${path.module}/user-data.sh", {
    server_port = var.port
    mysql_address  = data.terraform_remote_state.db.outputs.mysql_address
    mysql_port     = data.terraform_remote_state.db.outputs.mysql_port
  })
  
  lifecycle {
    create_before_destroy = true
  }
}

resource "aws_autoscaling_group" "example" {
  launch_configuration = aws_launch_configuration.example.name
  vpc_zone_identifier = data.aws_subnets.default.ids

  target_group_arns = [aws_lb_target_group.asg.arn]
  health_check_type = "ELB"

  min_size = var.min_size
  max_size = var.max_size

  tag {
    key                 = "Name"
    value               = "${var.cluster_name}-asg"
    propagate_at_launch = true
  }
}

resource "aws_security_group" "instance" {
  name = "${var.cluster_name}-instance"
}

resource "aws_security_group_rule" "allow_from_lb_inbound" {
  type                  = "ingress"
  security_group_id     = aws_security_group.instance.id

  from_port             = var.port
  to_port               = var.port
  protocol              = local.tcp_protocol
  cidr_blocks           = local.all_ips
}

resource "aws_security_group" "terrassh" {
    name = "${var.cluster_name}-ssh-access"
}

resource "aws_security_group_rule" "allow_ssh_inbound" {
  type                    = "ingress"
  security_group_id       = aws_security_group.terrassh.id

  from_port = 22
  to_port = 22
  protocol = "tcp"
  cidr_blocks = ["212.142.93.172/32","163.116.168.118/32"]

}

resource "aws_security_group" "alb" {
  name = "${var.cluster_name}-alb"
  
}

resource "aws_security_group_rule" "allow_http_inbound" {
  type                  = "ingress"
  security_group_id     = aws_security_group.alb.id

  from_port = local.http_port
  to_port = local.http_port
  protocol = local.tcp_protocol
  cidr_blocks = local.all_ips

}

resource "aws_security_group_rule" "allow_http_outbound" {
  type                  = "egress"
  security_group_id     = aws_security_group.alb.id

  from_port = local.any_port
  to_port = local.any_port
  protocol = local.any_protocol
  cidr_blocks = local.all_ips
  
}

resource "aws_lb" "example" {
  name = "${var.cluster_name}-asg"
  load_balancer_type = "application"
  subnets = data.aws_subnets.default.ids
  security_groups = [aws_security_group.alb.id]
}

resource "aws_lb_target_group" "asg" {
    name = "${var.cluster_name}-asg"
    port = var.port
    protocol = "HTTP"
    vpc_id = data.aws_vpc.default.id

    health_check {
      path = "/"
      protocol = "HTTP"
      matcher = "200"
      interval = 15
      timeout = 3
      healthy_threshold = 2
      unhealthy_threshold = 2
    }
}

resource "aws_lb_listener" "http" {
  load_balancer_arn = aws_lb.example.arn
  port = local.http_port
  protocol = "HTTP"

  default_action {
    type = "fixed-response"

    fixed_response {
      content_type = "text/plain"
      message_body = "404: page not found"
      status_code = 404
    }
  }
}

resource "aws_lb_listener_rule" "asg" {
  listener_arn = aws_lb_listener.http.arn
  priority = 100

  condition {
   path_pattern {
    values = ["*"]
   } 
  }

  action {
    type = "forward"
    target_group_arn = aws_lb_target_group.asg.arn
  }
}

data "terraform_remote_state" "db" {
  backend = "s3"

  config = {
    bucket = var.db_remote_state_bucket
    key    = var.db_remote_state_key
    region = "us-east-1"

  }
}

data "aws_vpc" "default" {
    default = true
}

data "aws_subnets" "default" {
    filter {
        name = "vpc-id"
        values = [data.aws_vpc.default.id]
    }
}
